plugins {
  id 'com.gradle.plugin-publish' version '2.0.0'
  id 'groovy'
}

group = 'br.com.hhs'
version = findProperty('app_version') ?: '0.1.0'

description = 'Gradle plugin for Python applications.'

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(21)
  }
}

repositories {
  mavenCentral()
}

dependencies {
  implementation gradleApi()
  implementation localGroovy()
  testImplementation gradleTestKit()
  testImplementation 'junit:junit:4.13.2'
}

tasks.register('ciTest') { task ->
  task.group = 'Verification'
  task.description = 'Run plugin tests for CI.'
  task.dependsOn 'validatePlugins'
  task.dependsOn 'test'
}

gradlePlugin {
  website = findProperty('siteUrl') ?: 'https://github.com/yorevs/py-gradle'
  vcsUrl = findProperty('siteUrl') ?: 'https://github.com/yorevs/py-gradle'
  plugins {
    pyGradle {
      id = 'br.com.hhs.pygradle'
      implementationClass = 'br.com.hhs.pygradle.PyGradlePlugin'
      displayName = 'PyGradle'
      description = 'Gradle plugin for Python applications.'
      tags.set(['python', 'build', 'automation'])
    }
  }
}

def publishKey = System.getenv('GRADLE_PUBLISH_KEY')
def publishSecret = System.getenv('GRADLE_PUBLISH_SECRET')
if (!project.hasProperty('gradle.publish.key') && publishKey) {
  project.ext['gradle.publish.key'] = publishKey
}
if (!project.hasProperty('gradle.publish.secret') && publishSecret) {
  project.ext['gradle.publish.secret'] = publishSecret
}
